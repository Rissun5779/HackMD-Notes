---
title: 數位系統與實習_數位邏輯
tags: [數位邏輯, Verilog]

---

:::info
**授課教師:黃炳森**
**參考書籍:Digital Logic Design Using Verilog, Vaibbhav Taraate**
[課本連結](https://link.springer.com/book/10.1007/978-981-16-3199-3)
:::
### 💾**目錄 (Table of Contents)**

- [**計數器 (Counter)**](#🥶計數器 (Counter))
- [**加法器 (Adder)**](#👾加法器(Adder))
- [**減法器 (Subtractor)**](#👻減法器(Subtractor))
- [**加減法器 (Adder-Subtractor)**](#😼加減法器 (Adder-Subtractor))
- [**乘法器 (Multiplier)**](#🎃乘法器 (Multiplier))
- [**多工器 (Multiplexer, MUX)**](#👹多工器 (Multiplexer, MUX))
- [**解碼器 (Decoder)**](#👽解碼器 (Decoder))
- [**編碼器 (Encoder)**](#編碼器 (Encoder))
- [**比較器 (Comparator)**](#🤙比較器(Comparator))
- [**閂鎖器 (Latch)**](#閂鎖器)
# 🥶**計數器 (Counter)** 
- **計數器是一種基本的數位電路元件，用於計算事件的發生次數或維護一個數字狀態。計數器通常可以根據其設計分為不同類型，主要包括同步計數器、異步計數器、向上計數器、向下計數器、以及雙向計數器。以下是這些計數器的簡介：**
  - **計數器的基本概念**
    **計數器的功能：計數器能夠計算時間、事件或脈衝的數量，並以數字形式表示這些數量。
    脈衝觸發：計數器通常是由時鐘信號觸發的，每當時鐘信號有變化（如上升沿或下降沿）時，計數器就會根據其設計的方式增加或減少計數值。**
    
### 🔴連波進位計數器(Ripple_Carry_Counter)
- **架構:**
&emsp;&emsp;**連波進位計數器由D型觸發器和一個反向器組成T型正反器再由，串聯T型正反器即可完成。**

![ripple-carry-counter](https://hackmd.io/_uploads/S1olo1H-Jx.png)

- **連波進位計數器主要模組**
```verilog=1
module Ripple_Carry_Counter(
  input  clock, reset, 
  output [3:0] q
);
  
  T_FF t0(clock, reset, q[0]),
       t1( q[0], reset, q[1]),
       t2( q[1], reset, q[2]),
       t3( q[2], reset, q[3]);
endmodule 
```
- **D型觸發器模組**
```verilog=1
module D_FF(
  input  clock, reset, d,
  output reg q
);
  
  always @(posedge reset or negedge clock)
    if(reset)
	   q=1'b0;
	 else 
	   q=d;
endmodule 
```
- **T型觸發器模組**
```verilog=1
module T_FF(
  input  clock, reset, 
  output q
);
  
  wire d;
  not(d, q);
  D_FF d1(clock, reset, d, q);
endmodule 
```
- **測試模組**
```verilog=1
`timescale 1ns/100ps
module stimulus;
  reg  clock, reset;
  wire [3:0] q;
  
  Ripple_Carry_Counter RC(clock, reset, q);
  initial 
    clock=0;
  always #5 clock=~clock;
  
  initial begin
         reset = 1;
	 #15  reset = 0;
	 #180 reset = 1;
	 #10  reset = 0;
	 #20 $finish;
  end
endmodule 
```

- **計數器的運作邏輯相對而言我覺得算是困難的，因為涉及了振盪之類的性質，某種程度上也算是有時序的影響，但是多加深入應用自然會有豁然開朗的一天!**
# 👾**加法器(Adder)** 
- **種類：加法器可分為基本的半加器，全加器（Ripple Carry Adder, CRA）、前瞻進位加法器（Carry Look-Ahead Adder, CLA）、進位選擇加法器（Carry Select Adder, CSA）等。**
**實務考量：在更高效能的應用中，會使用更複雜的設計如「跳躍進位」（Skip Carry）或「並行進位」（Parallel Carry）來進一步提升效率。**
### 🔵**半加法器(Half Adder)**
<div style="display: flex; justify-content: space-around;">
  <img src="https://hackmd.io/_uploads/r1cNZoX-ke.png" alt="圖片" width="60%">
  <img src="https://hackmd.io/_uploads/rkgCUjQZkl.png" alt="圖片" width="50%">
</div>

- **Boolean Function**
  $Sum=A⊕B$
  $Cout=A*B$
```verilog=1
module Half_Adder(
  input  A, B,
  output Sum, Cout
);
  
  xor ( Sum, A, B);
  and (Cout, A, B);
endmodule 
```
  &emsp;&emsp;**依據半法器的真值表和邏輯特性我們可以寫出以上的verilog代碼。**
  <br>
### 🔵**全加法器(Full Adder)**
<div style="display: flex; justify-content: space-around;">
  <img src="https://hackmd.io/_uploads/HySkdjX-Jl.png" alt="圖片" width="50%">
  <img src="https://hackmd.io/_uploads/Sk4VKoQ-Jx.png" alt="圖片" width="30%">
</div>

- **Boolean Function**
  $Sum=A⊕B⊕Cin$
  $Cout=AB+AC+BC$

```verilog=1
module Full_Adder(
  input  A, B, Cin,
  output Sum, Cout
);
  
 assign Sum  = A^B^Cin;
 assign Cout = A&B|A&Cin|B&Cin;
endmodule 
```

### 🔴**四位元連波進位加法器(4 bits Ripple Full Adder)**
![RCA](https://hackmd.io/_uploads/By6whoXZJg.jpg)
- **架構**
&emsp;&emsp;**由全加法器依序串聯而成，每一個全加器的進位都會成為下一個全加器的進位輸入之一，而同時每一個全加器都會輸入對應位元的A和B做輸入，並輸出對應位元的Sum。**
```verilog=1
module Ripple_Full_Adder(
  input  [3:0] A, B,
  output Cout,
  output [3:0] Sum
);
  //調用全加器(詳細請參閱全加器)
  wire [3:0] c;
  Full_Adder F0(A[0], B[0],   0, Sum[0], c[1]),
	     F1(A[1], B[1],c[1], Sum[1], c[2]),
	     F2(A[2], B[2],c[2], Sum[2], c[3]),
	     F3(A[3], B[3],c[3], Sum[3], Cout); 
endmodule
```
&emsp;&emsp;**實作上我們採用階層方的設計，運用上面的全加器電路設計，我們可以輕易的調用出我們需要的數量，但是要注意到每個輸入和輸出的對應關係是否正確。**
<br>

### 😈**前瞻進位加法器（Carry Look-Ahead Adder, CLA）**
<div style="display: flex; justify-content: space-around;">
  <img src="https://hackmd.io/_uploads/HJ-SWhm-1e.png" alt="圖片" width="50%">
  <img src="https://hackmd.io/_uploads/Bk9UWhmbye.png" alt="圖片" width="50%">
</div>

- **boolean function**

\begin{cases}
 C_1=G_0+P_0C_0 \\
 C_2=G_1+P_1G_0+P_1P_0C_0 \\
 C_3=G_2+P_2G_1+P_2P_1G_0+P_2P_1P_0C_0
\end{cases}
\begin{cases}
 P_i=A_i⊕B_i \\
 G_i=A_iB_i \\
 S_i=P_i⊕C_i \\
 C_{i+1}=G_i+P_iC_i
\end{cases}

![圖片](https://hackmd.io/_uploads/HJJ8PnQZ1l.png)

&emsp;&emsp;**理由為可以知道，$P_i和C_i$之間可以用來代換全加器，而其中$C_{i+1}=G_i+C_iP_i$尤其關鍵，$C_1、C_2、C_3..$皆可由此推導，以下是推導:**

\begin{cases}
 C_1=G_0+C_0P_0 \\
 C_2=G_1+C_1P_1=G_1+(G_0+C_0P_0)P_1 =G_1+P_1G_0+P_0P_1C_0\\
C_3=G_2+C_2P_2=G_2+(G_1+P_1G_0+P_0P_1C_0)P_2=G_2+P_2G_1+P_2P_1G_0+P_2P_1P_0C_0
\end{cases}

- **架構**
&emsp;&emsp;**實作上我們將全加法器拆開使用半加法器，以四位元為基礎，我們使用四個半加法器接收4位元數A和B同時輸出各位元的加總和進位，接著我們安裝上超前進位產生器，他可以依據剛剛推論的邏輯電路，高速的運算出各位元的進位數值。**

- CLA主要模組
```verilog=1
module CLA(
  input  [3:0] A, B,
  input  C0,
  output [3:0] Sum,
  output Cout
);
  
  wire [3:0] P, G, C;
  Half_adder Ha0(A[0], B[0], P[0], G[0]),
	     Ha1(A[1], B[1], P[1], G[1]),
	     Ha2(A[2], B[2], P[2], G[2]),
	     Ha3(A[3], B[3], P[3], G[3]);
  
  CLG clg(P, G, C0, C);
  xor (Sum[0], P[0], C0),
      (Sum[1], P[1], C[0]),
      (Sum[2], P[2], C[1]),
      (Sum[3], P[3], C[2]);
  
  buf (Cout, C[3]);  
endmodule 
```
- CLG模組
```verilog=1
module CLG(
  input  [3:0] P, G, C0,
  output [3:0] C
);
  
  wire [5:0] And;
  and (And[0], P[0], C[0]),
      (And[1], P[1], G[0]),
      (And[2], P[1], P[0], C[0]),
      (And[3], P[2], G[1]),
      (And[4], P[2], P[1], G[0]),
      (And[5], P[2], P[1], P[0], C[0]);
  
  or (C[0], G[0], And[0]),
     (C[1], G[1], And[1], And[2]),
     (C[2], G[2], And[3], And[4], And[5]);
endmodule 
```
- Half_adder模組
```verilog=1
module Half_adder(
  input  A, B,
  output Sum, Cout
);
  
  xor ( Sum, A, B);
  and (Cout, A, B);
endmodule  
```
- 測試模組
```verilog=1
`timescale 1ns/100ps
module stimulus;
  reg  [3:0] A, B;
  reg  C0;
  wire [3:0]Sum;
  wire Cout;
  
  
  CLA CA(A, B, C0, Sum, Cout);
  initial begin
    A=4'd0;B=4'd0;C0=0;
  end 
  always #50  A<=A+4'd2;
  always #100 B<=B+4'd3;
endmodule  
```
### 👿十進位加法器(Decimal Adder)
![圖片](https://hackmd.io/_uploads/S1XFvp4Wyg.png)
- **boolea function**
$Carry= Cout+Z_8Z_4+Z_8Z_2$
$S=Z+6$

- **在 BCD 加法中，當兩個 BCD 數字相加的結果大於 1001 時，實際上相當於二進制加法中得到的數值比預期少算了 6。這是因為 BCD 編碼的每個十進位數字只能表示 0 到 9，因此當二進位的和超過 9 時，我們需要將其調整回正確的範圍。**

![Decimal-Adder-BCD-Adder-6](https://hackmd.io/_uploads/rkeScaE-yg.jpg)

- **架構:**
&emsp;&emsp;**在邏輯電路設計的架構上，我們的路徑應該是，一個四位元的連波進位加法器，他會負責處理兩個輸入的計算<span style="color:red">(請注意此時計算的總和為二進位數)</span>，依據真值表我們知道總輸出的進位應該要如何計算，接著只要把計算的Z+Carry就可以轉換為十進位的BCD碼了。**

- **十進位加法器主要模組**
```verilog=1
module Decimal_Adder(
  input  [3:0] A, B,
  input  Cin, 
  output [3:0] S,
  output Cout
);
  
  wire [3:0] Z;
  wire C;
  Ripple_Full_Adder rfa0(A, B, Cin, C, Z);
  
  wire [3:0] c; 
  wire A1, A2, O;
  
   and (A1, Z[3], Z[2]),
       (A2, Z[3], Z[1]);
   or  (O, C, A1, A2);
	
  assign c = O ? 4'b0110 : 4'b0000;
  
  Ripple_Full_Adder rfa1(Z, c, 1'b0, Cout, S);
endmodule 
```

- **連波進位全加法器模組**
```verilog=1
module Ripple_Full_Adder(
  input  [3:0] A, B,
  input  Cin,
  output Cout,
  output [3:0] Sum
);
  
  wire [3:0] c;
  Full_Adder F0(A[0], B[0], Cin, Sum[0], c[1]),
				 F1(A[1], B[1],c[1], Sum[1], c[2]),
				 F2(A[2], B[2],c[2], Sum[2], c[3]),
				 F3(A[3], B[3],c[3], Sum[3], Cout); 
endmodule 
```

- **全加法器模組**
```verilog=1
module Full_Adder(
  input  A, B, Cin,
  output Sum, Cout
);
  
 assign Sum  =A^B^Cin;
 assign Cout =A&B|A&Cin|B&Cin;
endmodule 
```

- **測試模組**
```verilog=1
`timescale 1ns/100ps
module stimulus;
  reg  [3:0] A, B;
  reg  Cin; 
  wire [3:0]S;
  wire Cout;

  Decimal_Adder DA(A, B, Cin, S, Cout);
  initial begin
    A=4'd0;B=4'd0;Cin=0;
  end 
  always #50  A<=A+4'd2;
  always #100 B<=B+4'd3;
  always #400 Cin<=Cin+1;
endmodule 
```
- **由於該電路涉及多個元件，邏輯關係更為複雜，也更容易出錯，寫程式碼的時候絕對要非常注意和小心!**
# 👻減法器(Subtractor)
- **減法器（Subtractor）是一種數位邏輯電路，用於在二進制系統中執行減法運算。它和加法器一樣，是構建計算機和數位電路的基本組件之一。減法器可以根據應用需求分為「半減法器（Half Subtractor）」和「全減法器（Full Subtractor）」。**

### 🔵半減法器(Half Subtractor)
<div style="display: flex; justify-content: space-around;">
  <img src="https://hackmd.io/_uploads/S1FZgvNbyl.png" alt="圖片" width="70%">
  <img src="https://hackmd.io/_uploads/HkzhgPEZke.png" alt="圖片" width="40%">
</div>

- **boolean function**
 $Diif=A⊕B$
 $Borr=A'B$

```verilog=1
module Half_Subtractor(
  input  A, B,
  output Diff, Borr
);
  
  wire BA;
  not (  BA, A);
  xor (Diff,  A, B);
  and (Borr, BA, B);
endmodule 
```

&emsp;&emsp;**實作上是採用類似半加器的邏輯，唯一的差異就是借位時候輸入BA而不是A。**

### 🔴全減法器(Half Subtractor)
<div style="display: flex; justify-content: space-around;">
  <img src="https://hackmd.io/_uploads/SJCNED4b1l.png" alt="圖片" width="70%">
  <img src="https://hackmd.io/_uploads/HJV8SwVbJl.png" alt="圖片" width="35%">
</div>

- **boolean function**
 $Diif=A⊕B⊕Bin$
 $Borr=A'B+Bin(\overline{A⊕B})$

- **全減器模組**
```verilog=1
module Full_Subtractor(
  input  A, B, Bin,
  output Diff, Bout
);
  
  wire A1, BA, A2, X2, BX2;
  
  xor (Diff, A, B, Bin),  
      (  X2, A, B);
  not (  BA,  A),
      ( BX2, X2);
  and (  A1, BA,    B),
      (  A2, Bin, BX2);
  or  (Bout, A1, A2);
endmodule 
```

- **測試模組**
```verilog=1
`timescale 1ns/100ps
module stimulus;
  reg  A, B, Bin;
  wire Diff, Bout;
  
  Full_Subtractor FS(A, B, Bin, Diff, Bout);
  initial begin
    A=0;B=0;Bin=0;
  end
  always #100 A<=A+1;
  always #200 B<=B+1; 
endmodule 
```
&emsp;&emsp;**實作上是採用類似全加器的邏輯，但是為了因應borr的邏輯電路，做了比較多的wire去處理，也因此更容易出錯，需要特別留意!**

### 😈連波借位全減法器(Ripple Borrow Full Subtractor)
![圖片](https://hackmd.io/_uploads/BJ2ayKE-1x.png)

- **架構:**
&emsp;&emsp;**實作方式主要就是串連全減法器，每一位的進位都會被傳入下一位，同時也會輸出每一位的差。** 

- **連波全減法器模組**
```verilog=1
module Ripple_Full_Subtractor(
  input  [3:0] A, B,
  input  Bin, 
  output [3:0] Diff,
  output Bout
);
  
  wire [2:0] Bo;
  Full_Subtractor FS0(A[0], B[0],   Bin, Diff[0], Bo[0]),
		  FS1(A[1], B[1], Bo[0], Diff[1], Bo[1]),
		  FS2(A[2], B[2], Bo[1], Diff[2], Bo[2]),
		  FS3(A[3], B[3], Bo[2], Diff[3], Bout);
endmodule 
```
- **全減器模組**
```verilog=1
module Full_Subtractor(
  input  A, B, Bin,
  output Diff, Bout
);
  
  wire A1, BA, A2, X2, BX2;
  
  xor (Diff, A, B, Bin),  
      (  X2, A, B);
  not (  BA,  A),
      ( BX2, X2);
  and (  A1, BA,    B),
      (  A2, Bin, BX2);
  or  (Bout, A1, A2);
endmodule 
```

- **測試模組**
```verilog=1
`timescale 1ns/100ps
module stimulus;
  reg  [3:0] A, B;
  reg  Bin;
  wire [3:0] Diff;
  wire Bout;
  
  Ripple_Full_Subtractor rfs(A, B, Bin, Diff, Bout);
  initial begin
        A=4'd0;B=4'd0;Bin=0;	
  end
  
  always #50  A<=A+4'd3;
  always #100 B<=B+4'd2;
endmodule 
```

&emsp;&emsp;**實作上是採用類似連波全加器的邏輯，需要同時參照連波進位加法器和全減器**<br>

# 😼加減法器 (Adder-Subtractor)
- **加減法器（Adder-Subtractor）是一種可以同時執行加法與減法運算的數位電路。在電子工程和數位電路中，加減法器是一個非常重要的元件，常用於計算器、處理器中的算術邏輯單元（ALU），以及各種嵌入式系統中。其運作主要基於全加法器（Full Adder）電路，並利補碼（Two's Complement）來實現減法。**
![圖片](https://hackmd.io/_uploads/BkfPK3EWJl.png)
- **架構:**
&emsp;&emsp;**就是主要運用全加法器搭配二的補數的運算概念，因為A-B等同於A+B(B做二的補數)得到的數值，最後，就如同連波進位器我們也需要一個輸出來表達整體最終的進位到下一個運算中，我們可以藉由Cin和Cout的邏輯電路來判斷，當Cin為1時候代表兩數是在做減法，反之同理，但是最後就會注意到計算出來經常會有一個溢位的錯誤需要修正，關於這點我們<span style="color:red">採用xor(Cout,Cin)即可處理。</span>**

- **加減法器主要模組**
```verilog=1
module Adder_Subtractor(
  input  [3:0] A, B,
  input  M,
  output [3:0] Sum,
  output Cout, Verify
);
    //使用XOR是因為根據數位邏輯的原理，B'M+BM' 將會是輸出可以產生加減法的效果。
  wire [3:0] C, b;
  xor (b[0], B[0], M),
      (b[1], B[1], M),
      (b[2], B[2], M),
      (b[3], B[3], M),
      (Verify, Cout, C[2]);
		
  Full_Adder FA0(A[0], b[0],    M, Sum[0], C[0]),
	     FA1(A[1], b[1], C[0], Sum[1], C[1]),
	     FA2(A[2], b[2], C[1], Sum[2], C[2]),
             FA3(A[3], b[3], C[2], Sum[3], Cout);  
   
endmodule 
```

- **全加器模組**
```verilog=1
module Full_Adder(
  input  A, B, Cin,
  output Sum, Cout
);
  
  wire A1, A2, A3;
  xor (Sum, A, B, Cin);
  
  and (A1, A,  B),
      (A2, A,Cin),
      (A3, B,Cin);
    
  or  (Cout, A1, A2, A3);
endmodule 
```

- **測試模組**
```verilog=1
`timescale 1ns/100ps
module stimulus;
  reg  [3:0] A, B;
  reg  M;
  wire [3:0] Sum;
  wire Cout, Verify;
  
  Adder_Subtractor AS(A, B, M, Sum, Cout, Verify);
  
  initial begin
          A=4'b1111;B=4'b0000;M=1;
     #100 A=4'b1100;B=4'b0011;M=1;
     #100 A=4'b0011;B=4'b1100;M=1;
     #100 A=4'b0101;B=4'b0101;M=0;
     #100 A=4'b1010;B=4'b0101;M=0;
     #100 A=4'b1101;B=4'b1010;M=0;
  end
endmodule 
```

- **參考波形圖:**
![圖片](https://hackmd.io/_uploads/HkbxxpEZke.png)

# 🎃乘法器 (Multiplier)

- **在數位邏輯中，乘法器的目的是計算兩個數的乘積。對於二進制數，乘法的過程類似於十進制的乘法，透過逐位相乘和進位的方式來計算結果。即圖1**
### 🔴二進位乘法器
<div style="display: flex; justify-content: space-around;">
  <img src="https://hackmd.io/_uploads/HyyldC4bJl.png" alt="圖片" width="70%">
  <img src="https://hackmd.io/_uploads/rygKzd04bke.png" alt="圖片"width="50%">
</div>

- **二進位乘法器主要模組**
```verilog=1
module Binary_Multiplier(
  input  [1:0] A, B,
  output [3:0] C
);
  //在22乘法器的and邏輯實現，運來做接下的運算。
  wire A1, A2, A3;
  and (C[0], A[0], B[0]),
      (  A1, A[0], B[1]),
      (  A2, A[1], B[0]),
      (  A3, A[1], B[1]);
  //引用半加法器單元來做運算，詳細可以參閱數位邏輯
  wire carry; 
  Half_Adder HA1(A1,    A2, C[1], carry),
	     HA2(A3, carry, C[2],  C[3]);
endmodule 
```
- **全加法器(省略)**
- **測試模組**
```verilog=1
`timescale 1ns/100ps
module Mul22_stimulus;
  reg  [1:0] A, B;
  wire [3:0] C;
  //測試程式碼用以測試正確性
  Binary_Multiplier BM(A, B, C);
  initial begin
       A=2'b0;B=2'b0;
  end
  always #50 A<=A+1;
  always #200 B<=B+1;
  
  initial 
    $monitor($realtime, " ns, A= %d, B= %d, C= %d", A, B, C);
endmodule  
```

&emsp;&emsp;**乘法器相對而言其實不是非常複雜，但是一旦位數更多了如何處理將會是一個值得留意的挑戰!**

# 👹多工器 (Multiplexer, MUX)
![n_onemultiplexer_blockdiagram](https://hackmd.io/_uploads/HJ-rexSW1x.jpg)

- **多工器（Multiplexer，簡稱 MUX）和解多工器（Demultiplexer，簡稱 DEMUX）是數位電路中兩種重要的組件。它們在功能上是相對的，主要差異如下：**

- **多工器 (MUX)
    功能：多工器是一個選擇器，它根據控制信號選擇一個輸入信號並將其轉換為單個輸出。換句話說，MUX 會將多個輸入信號合併成一個輸出信號。
    輸入與輸出：有多個數據輸入（如 2n2n 個）和一個輸出。控制信號的數量 nn 用於選擇哪一個輸入被傳遞到輸出。
    示例：一個 2-to-1 多工器有兩個輸入、一個輸出和一個控制信號。根據控制信號的值，選擇其中一個輸入作為輸出。**
    
- **解多工器 (DEMUX)
    功能：解多工器的功能正好與多工器相反。它接收單個輸入信號並根據控制信號將其轉發到多個輸出中。也就是說，DEMUX 可以將一個數據信號分配到多個輸出。
    輸入與輸出：有一個數據輸入和多個數據輸出（如 2n2n 個）。控制信號的數量 nn 用於選擇將數據輸出到哪一個具體的輸出端。
    示例：一個 1-to-2 解多工器有一個輸入、兩個輸出和一個控制信號。根據控制信號的值，將輸入信號轉發到其中一個輸出。**

- **總結
    多工器 是用來選擇輸入信號並將其轉化為單一輸出；而 解多工器 則是用來將單一輸入信號分配到多個輸出。
    在實際應用中，多工器通常用於數據路由，而解多工器則用於數據分發。**

### 2 to 1 多工器
![圖片](https://hackmd.io/_uploads/SkJlWgHWyg.png)

- **2-1多工器主要模組**
```verilog=1
module MUL2_1(A, B, S, F);

	input A, B, S;
	output F;
	
	assign F = ~S & A | S & B;

endmodule
```

- **測試模組**
```verilog=1
`timescale 1 ns/100 ps
module Test_tb;
reg A, B, S;
wire F;

MUL2_1 UUT(.A(A), .B(B), .S(S), .F(F));
// MUL2_1 UUT(A, B, S, F);

initial begin
	A = 1'b0; B = 1'b0; S = 1'b0;
	#100 B = 1'b1;
   #200 B = 1'b0; S = 1'b1;
   #100 B = 1'b1;
	#100 B = 1'b0;
	#100 S = 1'b0;
   #100 B = 1'b1; 	
end

always #100 A <= A + 1;  

initial #800 $finish;
initial $monitor($realtime, "ns %h %h %h %h", A, B, S, F);  

endmodule
```

### 4-to-1 多工器

![VwgyX5p](https://hackmd.io/_uploads/rkf4fer-1l.png)
![圖片](https://hackmd.io/_uploads/BkRHfxSWye.png)

- **4-1多工器主要模組**
```verilog=1
module MUL_4(I, S, F);
  input [3:0] I;
  input [1:0] S;
  output F;
  
  wire s1n, s0n;
  wire y0, y1, y2, y3;
  
  not S1n(s1n, S[1]);
  not S0n(s0n, S[0]);
  
  and O0(y0, I[0], s1n, s0n);
  and O1(y1, I[1], s1n, S[0]);
  and O2(y2, I[2], S[1], s0n);
  and O3(y3, I[3], S[1], S[0]);
  
  or out(F, y0, y1, y2, y3); 
endmodule 
```

- **測試模組**
```verilog=1
`timescale 1ns/100ps
module MUL_4_tb;
  reg [3:0] I;
  reg [1:0] S;
  wire F;
  
  MUL_4 ml(I, S, F);
  initial begin
    I=4'b0000;S=2'b00;
	 #1200 I=4'b1111;
  end
  
  always #100 I<=I+1'b1;
  always #300 S<=S+1'b1;
  
  initial begin
    #1600 $finish;
	 $monitor($realtime, "ns %h %h %h", I, S, F);
  end 
endmodule 
```

### 1-to-4 解多工器
![圖片](https://hackmd.io/_uploads/rkY201S-1x.png)

- **1-4解多工器主要模組**
```verilog=1
module MUL_1_to_4(I, S0, S1, F0, F1, F2, F3);
  input  I;
  input  S0, S1;
  output F0, F1, F2, F3;
  
  wire s1n, s0n;
  not (s0n, S0);
  not (s1n, S1);
  
  and(F0, I, s1n, s0n);
  and(F1, I, s1n, S0);
  and(F2, I, S1, s0n);
  and(F3, I, S1, S0);
endmodule 
```

- **測試模組**
```verilog=1
`timescale 1ns/100ps
module MUL_1_to_4_tb;
  reg I, S0, S1;
  wire F0, F1, F2, F3;
  
  MUL_1_to_4 ml(I, S0, S1, F0, F1, F2, F3);
  initial 
    begin
	       S0=0;S1=0;I=1;
	  #200 I<=0;
    end
  
  always #50  S0<=S0+1;
  always #100 S1<=S1+1;
  
  initial #400 $finish;
endmodule 
```
- **輸入訊號I依據選擇線的開關被控制導入到特定的輸出口。**
# 👽解碼器 (Decoder)
- **解碼器（Decoder） 是一種數位邏輯電路，它將二進制數字（通常是n位元）轉換為唯一的輸出信號，通常在一組2^n個輸出中只會有一個輸出為高電平（1），其餘則為低電平（0）。解碼器廣泛應用於計算機系統、存儲器解碼、數據選擇等領域。**
### 🔵二對四解碼器(Decoder2_to_4)
![decoder-300x169](https://hackmd.io/_uploads/HyW28ySb1g.png)

- **2-4解碼器主要模組**
```verilog=1
module Decoder2_to_4(
  input  A, B, E,
  output [3:0] D
);
  
  wire BA, BB, BE;
  not  (BA, A),
       (BB, B),
       (BE, E);
  
  nand (D[0], BA, BB, BE),
       (D[1], BA,  B, BE),
       (D[2],  A, BB, BE),
       (D[3],  A,  B, BE);
endmodule 
```
- **測試模組**
``` verilog=1
`timescale 1ns/100ps
module Decoder2_to_4_tb;
  reg  A, B, E;
  wire [3:0] D;
  
  Decoder2_to_4 De(A, B, E, D);
  initial begin
      A=0; B=0; E=0;
  end
  always #100 B<=B+1;
  always #200 A<=A+1;
  always #400 E<=E+1;
endmodule 
```
![圖片](https://hackmd.io/_uploads/rJS0SJrZ1x.png)
![圖片](https://hackmd.io/_uploads/H1Var1SZkx.png)

- **3-8 解碼器主要模組**
```verilog=1
module Decoder3_to_8(
  input  A, B, C, E, // 增加 C 為輸入
  output [7:0] D    // 增加輸出為 8 位元
);

  wire BA, BB, BC, BE;
  not  (BA, A),
       (BB, B),
       (BC, C),
       (BE, E);
  
  // 生成8個輸出
  nand (D[0], BA, BB, BC, BE), // 000
       (D[1], BA, BB,  C, BE), // 001
       (D[2], BA,  B, BC, BE), // 010
       (D[3], BA,  B,  C, BE), // 011
       (D[4],  A, BB, BC, BE), // 100
       (D[5],  A, BB,  C, BE), // 101
       (D[6],  A,  B, BC, BE), // 110
       (D[7],  A,  B,  C, BE); // 111
endmodule 

```
- **測試模組**
``` verilog=1
`timescale 1ns/100ps
module Decoder3_to_8_tb;
  reg  A, B, C, E;
  wire [7:0] D; // 將輸出改為 8 位元
  
  Decoder3_to_8 De(A, B, C, E, D);
  initial begin
      A = 0; B = 0; C = 0; E = 0; // 初始化所有輸入
  end
  
  always #100 C <= ~C; // 每100ns反轉 C
  always #200 B <= ~B; // 每200ns反轉 B
  always #400 A <= ~A; // 每400ns反轉 A
  always #800 E <= ~E; // 每800ns反轉 E
endmodule 
```

&emsp;&emsp;**在概念和功能上解碼器很好理解，就是按照自己的需求可以藉由AND閘去設計類似開關的設計，輸入的邏輯會對應到特定的開關，關鍵在於如何在未來靈活應用到設計中。**

# 編碼器 (Encoder)

# 🤙**比較器(Comparator)** 
- **種類:比較器、幅度比較器(Magnitude Comparator)etc.**
- **比較器（Comparator）是一種用於比較兩個數位信號的電路。它可以確定兩個輸入值之間的大小關係，並根據結果輸出相應的信號。比較器通常用於數據處理、數字控制系統和其他需要進行數據比較的應用。**

### 🔵比較器(Comparator)
![圖片](https://hackmd.io/_uploads/BJJfWkHbyx.png)

- **boolean function**
$F_1=A'B$
$F_2=\overline{F_1+F_2}$
$F_3=AB'$

- **比較器主要模組**
```verilog=1
module Comparator(
  input  A, B,
  output FA, FB, FX
);
  
  wire BA, BB;
  not (BA, A), (BB, B);
  
  and (FA, BA, B),(FB, BB, A);
	
  nor (FX, FA, FB);
endmodule 
```

### 🔴大小比較器(Magnitude Comparator)
![圖片](https://hackmd.io/_uploads/BJaaGyrb1l.png)

- **架構:**
&emsp;&emsp;**輸入兩個四位元的二進位數比較他們的大小，邏輯上採用四個比較器分別處理四個位元的邏輯運算，然後因為他們最終要被統一為一個判斷大小的輸出，參考以上圖片完成線路串接即可得到一個可以達成此功能的電路**

- **大小比較器主要模組**
```verilog=1
module Comparator4_to_4(
  input  [3:0]A, B,
  output ALB, AGB, AEB
);
  
  wire [3:0] FA, FB, FX;
  wire [6:0] AND;
  
  Comparator C3(A[3], B[3], FA[3], FB[3], FX[3]),
	     C2(A[2], B[2], FA[2], FB[2], FX[2]),
	     C1(A[1], B[1], FA[1], FB[1], FX[1]),
	     C0(A[0], B[0], FA[0], FB[0], FX[0]);
  
  and (AND[6], FX[3], FA[2]),
		(AND[5], FX[3], FB[2]),
		(AND[4], FX[3], FX[2], FA[1]),
		(AND[3], FX[3], FX[2], FB[1]),
		(AND[2], FX[3], FX[2], FX[1], FA[0]),
		(AND[1], FX[3], FX[2], FX[1], FB[0]),
		(   AEB, FX[3], FX[2], FX[1], FX[0]);
  
  or  (ALB, FA[3], AND[6], AND[4], AND[2]),
      (AGB, FB[3], AND[5], AND[3], AND[1]);
endmodule 
```

- **比較器模組**
```verilog=1
module Comparator(
  input  A, B,
  output FA, FB, FX
);
  
  wire BA, BB;
  not (BA, A), (BB, B);
  
  and (FA, BA, B),(FB, BB, A);
	
  nor (FX, FA, FB);
endmodule 
```

- **測試模組**
```verilog=1
`timescale 1ns/100ps
module Comparator4_to_4_tb;
  reg  [3:0] A, B;
  wire ALB, AGB, AEB;
  
  Comparator4_to_4 Comp4(A, B, ALB, AGB, AEB);
  initial begin
        A=4'd1;B=4'd3;
	 #50 A=4'd8;B=4'd8;
	 #50 A=4'd3;B=4'd6;
	 #50 A=4'd8;B=4'd4;
	 #50 A=4'd2;B=4'd9;
	 #50 ;
  end
endmodule 
```

- **比較器整體的設計，就是如同前面的設計思路。**

### 閂鎖器
- **直到輸入信號切換狀態之前，都可以無限期的維持當前狀態，各種閂鎖與正反器的主要差異在於，輸入數以及這些輸入影響其狀態的方式，而閂鎖屬最基本的元件。**
![maxresdefault](https://hackmd.io/_uploads/SJTFXuHfye.jpg)
```verilog=1
module La(
  input  Set, Reset,
  output reg Q, BQ
);
  
  always@(*)begin
	   if(Reset&&Set)begin
		  Q=Q;
		  BQ=BQ;
		end
		else if(Reset)begin
		  Q=0;
		  BQ=1;
		end
		else if(Set)begin
		  Q=1;
		  BQ=0;
		end
	 end
endmodule 
```

- **這是基本的SR閂鎖器電路實現**
```verilog=1
module La_Nand(
  input  Set, Reset,
  output reg Q, BQ
);
  
  wire B_Set, B_Reset;
  assign B_Set=~Set;
  assign B_Reset=~Reset;
			
  always@(*)begin
      Q=~(BQ&B_Set);
      BQ=~( Q&B_Reset);
  end
endmodule 
```
- **這是NAND型式的實現方式**